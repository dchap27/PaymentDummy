apply plugin: 'com.android.application'
apply plugin: 'signing'

def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

// create versioning formula
ext {
    versionMajor = 1
    versionMinor = 2
    versionPatch = 0
    screenSize = 0
    minimumSdkVersion = 18
    versionClassifier = 'opener'
    isSnapShot = true
}

android {
    signingConfigs {
        paymentRelease {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 33
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "ng.com.dayma.paymentdummy"
        minSdkVersion minimumSdkVersion
        targetSdkVersion 33
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.paymentRelease
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
    }
    flavorDimensions "version"

    productFlavors {
        regular {
            dimension "version"
            applicationIdSuffix ".regular"
            resValue "string", "content_provider", applicationId + ".provider"

        }
        advanced {
            dimension "version"
            applicationIdSuffix ".advanced"
            resValue "string", "content_provider", applicationId + ".provider"
        }
    }
}

private Integer generateVersionCode(){
    return minimumSdkVersion * 10000000 + versionMajor * 10000 + versionMinor * 100 + versionPatch
}

private String generateVersionName(){
    String version_Name = "${versionMajor}.${versionMinor}.${versionPatch}"
    if(versionClassifier == null & isSnapShot){
        versionClassifier = "SNAPSHOT"
    }
    if(versionClassifier != null){
        version_Name += "-" + versionClassifier
    }
    return version_Name
}

dependencies {
    def lifecycle_version = "1.1.1"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    // ViewModel and LiveData
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0'
    androidTestImplementation 'androidx.appcompat:appcompat:1.0.0'
    androidTestImplementation 'com.google.android.material:material:1.0.0'
    androidTestImplementation 'androidx.cardview:cardview:1.0.0'

}
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            // Skip multidex because it follows a different versioning pattern.
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '25.3.1'
            }
        }
    }
}